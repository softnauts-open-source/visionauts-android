apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.akaita.android.easylauncher'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.softnauts.visionauts"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "server"

    productFlavors {
        staging {
            dimension "server"
            buildConfigField "String", "API_URL", '"http://yourStagingApiUrl.com/"'
            applicationIdSuffix ".staging"
        }

        production {
            dimension "server"
            buildConfigField "String", "API_URL", '"http://yourProductionApiUrl.com/"'
        }
    }

    dataBinding {
        enabled = true
    }
}

easylauncher {
    variants {
        productionRelease {
            filters = []
        }
        productionDebug {
            filters = greenRibbonFilter("debug")
        }
        stagingRelease {
            filters = grayscaleFilter()
        }
        stagingDebug {
            filters = [
                    grayscaleFilter(),
                    greenRibbonFilter("debug")
            ]
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Androidx
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.viewpager:viewpager:1.0.0'

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    androidTestImplementation "com.google.dagger:dagger:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    // Architecture Components
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0-alpha02"
    kapt "androidx.room:room-compiler:2.1.0-alpha04"
    implementation "androidx.room:room-rxjava2:2.1.0-alpha04"

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Stetho
    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    // Time and date
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation 'net.danlew:android.joda:2.10.1.1'

    // Pulse layout
    implementation 'pl.bclogic:pulsator4droid:1.0.3'

    // EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // Kontakt.io
    implementation "com.kontaktio:sdk:4.0.0"

    // Dialogs
    implementation 'com.afollestad.material-dialogs:core:2.0.0'

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.2-alpha01'
    testImplementation 'org.robolectric:robolectric:3.6.1'
    testImplementation 'org.mockito:mockito-core:2.7.22'
}
